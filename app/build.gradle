plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'jacoco'
    id 'com.google.firebase.appdistribution'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
}

def buildPropFile = rootProject.file('build.properties')
Properties buildProps = new Properties()
buildProps.load(new FileInputStream(buildPropFile))

android {
    compileSdk 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.lethalmaus.exampleandroidproject"
        minSdk 23
        targetSdk 31
        versionCode buildProps["VERSION_CODE"] as Integer
        versionName buildProps["VERSION_NAME"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testCoverage.jacocoVersion = "0.8.7"
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions "environment"
    productFlavors {
        mock {
            dimension "environment"
            applicationIdSuffix ".mock"
        }

        production {
            dimension "environment"
            firebaseAppDistribution {
                releaseNotesFile="./release-notes.txt"
            }
        }
    }

    lintOptions {
        abortOnError true
        ignoreWarnings false
        checkAllWarnings true
        warningsAsErrors false
        enable 'StopShip'
        fatal 'StopShip'
    }

    android.variantFilter { variant ->
        if (variant.getName() == 'mockRelease') {
            variant.setIgnore(true)
        }
    }
}

android.applicationVariants.all { variant ->
    def googleTask = tasks.findByName("process${variant.name.capitalize()}GoogleServices")
    googleTask.enabled = "mock" != variant.flavorName
}

def exclusions = ['**/R.class',
                  '**/R$*.class',
                  '**/Manifest*.*',
                  '**/android/databinding/*',
                  'android/**/*.*',
                  '**/BuildConfig.*',
                  '**/*$ViewBinder*.*',
                  '**/*$ViewInjector*.*',
                  '**/Lambda$*.class',
                  '**/Lambda.class',
                  '**/*Lambda.class',
                  '**/*Lambda*.class',
                  '**/models/**'
]
jacoco {
    toolVersion = "0.8.7"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task ("jacocoDebugReport", type: JacocoReport, dependsOn: ["createDebugCoverageReport"]) {
    group 'Reporting'
    description "Generate debug Jacoco coverage reports."

    reports {
        xml.enabled = true
        html.enabled = true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: exclusions)
        }))
    }

    additionalSourceDirs.setFrom(files("src/main/java"))
    sourceDirectories.setFrom(files("src/main/java"))
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "com.github.bumptech.glide:glide:4.12.0"
    kapt "com.github.bumptech.glide:compiler:4.12.0"
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.google.android.play:core-ktx:1.8.1'
    implementation platform('com.google.firebase:firebase-bom:29.0.4')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.4.0"
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
}